plugins {
	id 'application'
	id 'checkstyle'
	id 'jacoco'
	id 'org.springframework.boot' version '3.0.3'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.adarshr.test-logger' version '2.1.1'
	id 'com.github.ben-manes.versions' version '0.38.0'
	id 'org.liquibase.gradle' version '2.0.4'
	id 'org.siouan.frontend-jdk11' version '6.0.0'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'

application {
	mainClass = 'hexlet.code.AppApplication'
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok'

	annotationProcessor(
			'org.projectlombok:lombok',
			'com.querydsl:querydsl-apt:5.0.0:jakarta',
			'jakarta.persistence:jakarta.persistence-api:3.1.0'
	)

	implementation(
			'org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-security',
			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.boot:spring-boot-starter-web',
			'io.jsonwebtoken:jjwt-api:0.11.5',
			'io.jsonwebtoken:jjwt-impl:0.11.5',
			'io.jsonwebtoken:jjwt-jackson:0.11.5',
			'org.liquibase:liquibase-core',
			'org.springdoc:springdoc-openapi-starter-webmvc-api:2.0.2',
			'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2',
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	)

	runtimeOnly (
			'com.h2database:h2:2.1.214',
			'org.postgresql:postgresql:42.2.24',
			'io.jsonwebtoken:jjwt-jackson:0.11.5'
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test',
			'org.springframework.security:spring-security-test'
	)

	liquibaseRuntime(
			sourceSets.main.output,
			'com.h2database:h2:2.1.214',
			'org.liquibase:liquibase-core:4.19.0',
			'org.liquibase.ext:liquibase-hibernate6:4.19.0',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'info.picocli:picocli:4.7.1'
	)
}

test {
	useJUnitPlatform()
}

compileJava {
	options.release = 17
	options.encoding = 'UTF-8'
}

wrapper {
	gradleVersion = '7.6'
	distributionType = Wrapper.DistributionType.ALL
}

diffChangeLog {
	dependsOn compileJava
}

testlogger {
	showStandardStreams = true
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

checkstyle {
	toolVersion '9.0'
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			url 'jdbc:h2:./taskManager'
			username ''
			password ''
			referenceUrl 'hibernate:spring:hexlet.code.model.' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy'
		}
	}
}

frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
	outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
	configure configFrontendTasks

	doLast {
		copy {
			from "$projectDir/frontend/build"
			into "$buildDir/resources/main/static"
		}
	}
}

installFrontend {
	configure configFrontendTasks
}

processResources.dependsOn assembleFrontend